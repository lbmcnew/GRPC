model {
  # -------------------------------
  # parameter priors
  # -------------------------------

  # After-second-year (Adult, ASY) females
  na ~ dnorm(150, 0.01)T(0,)
  N.a[1] <- round(na)

  # Immigrant females
  ni ~ dnorm(150, 0.01)T(0,)
  N.i[1] <- round(ni)

  sig.numImms ~ dunif(0, 10)
  tau.numImms <- pow(sig.numImms, -2)

  m.numImms ~ dunif(0, 100)

  l.m.numImms <- log(m.numImms)

  for (t in 1:(N.YEARS-1)) {
  	epsilon.numImms[t] ~ dnorm(0, tau.numImms)
  	log(numImms[t]) <- l.m.numImms + epsilon.numImms[t]
  }

  for (t in 1:(N.YEARS-1)) {
    f[t] <- ((N.PROB * C.1.SIZE[t] * N.SURV.1[t] * HS.1[t]) +
              (1 - N.SURV.1[t]) * (RE.N.PROB * C.2.SIZE[t] * N.SURV.2[t] * HS.2[t])) *
            B.SURV[t] * FS[t] * 0.5

    # NOTE immigrants are assumed to have the same fecundity and survival
    s.i[t] <- S.A[t]
  }
  
  # -------------------------------
  # derived parameters
  # -------------------------------

  for (t in 1:(N.YEARS-1)) {
  	omega[t] <- N.i[t+1] / N.tot[t]
  	lambda[t] <- N.tot[t+1] / N.tot[t]
  }

  # -------------------------------
  # processes
  # -------------------------------

  # State process
  for (t in 2:N.YEARS) {
  	Fl.mean[t-1] <- (N.tot[t-1] * f[t-1])
  	Fl.tot[t-1] ~ dpois(Fl.mean[t-1])

    Y.Surv[t] ~ dbin(S.J[t-1], Fl.tot[t-1])
  	A.Surv[t] ~ dbin(S.A[t-1], N.a[t-1])
  	I.Surv[t] ~ dbin(s.i[t-1], N.i[t-1])

  	N.a[t] <- Y.Surv[t] + A.Surv[t] + I.Surv[t]
  	N.i[t] ~ dpois(numImms[t-1])
  }

  # Observation process
  for (t in 1:N.YEARS) {
  	N.tot[t] <- N.a[t] + N.i[t]

  	COUNTS[t] ~ dpois(N.tot[t] * P.SURVEYED[t])
  }
}